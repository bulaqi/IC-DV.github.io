### 1. 基础知识
#### 1. 常用伪随机的产生
1. 概率分布函数
   - $random,
   - $dist_uniform,
   - $dist_normal,
   - $dist_exponential,
   - $dist_poisson
2. 对象随机方法
   - 所有SV类中都会默认存在的内置虚函数（原型是virtual function int randomize()），但是它不能被覆盖（overridden）
   - 使用object.randomize() 来对对象进行随机化的时候，注意它只会随机化类中有rand关键词修饰的成员变量，并且在成功随机化之后会返回1，失败则返回0
   - 类中还有randomize的两个回调函数pre_randomize()和post_randomize()
   - 补充说明: object.randomize()还是只能随机化类的成员变量，不能随机化局部变量
   - Std lib下的std::randomize()的适用性比object.randomize()要好，不过它不能自动随机对象中的rand成员变量，也没有pre和post函数可以调用
3. 随机系统函数
   - $urandom()
   - $urandom_range()
4.  数组的shuffle()
   - 使得数组的元素随机排序，某些场景下可以快速获取不重复序列，
   - 但是不推荐使用，此方法太过于占用内存资源
     
#### 2. 随机稳定性理论
1. 随机的稳定性
   在SV中，不同线程（thread）或对象（object）在随机化时使用的随机数产生器（RNG）是相互独立的。相同线程或相同对象在相同随机种子的情况下，每一次仿真中产生的随机数序列是一样的
2. Initialization RNG：
   - 初始化RNG是产生随机数的开始，用来给RNG初始化随机种子。
   - 每一个模块实例（module instance）、接口实例（interface instance）、程序块（program）和包（package）实例都有属于自己的初始化RNG，在不指定随机种子的情况下，默认的随机种子根据不同编译器的实现决定的。
3. Hierarchy seeding：
   - 分层分配随机种子是随机稳定性的重要机制。
   - 在创建新的线程或者实例化对象的时候，父线程使用的RNG的下一个随机值会作为这个新线程或者新对象的RNG的随机状态，即作为新的种子传递下去。
4.Thread and Object Stability：
   - SV中将程序（program）、模块（module）、接口（interface）、函数（function）、任务（task）等这些独立的块叫Process。
   - 每个Process都有自己的RNG。每个RNG都有自己的随机状态（random state）。
   - 可以通过process::self()这个静态方法获取当前Process的RNG句柄，在通过句柄调用get_randstate()方法来获得随机状态。
   - 不同的仿真工具返回来的随机状态的值的表现方式可能会不一样，但基本都是一段看起来没有规律的字符串，这个字符串表示下一个要产生的随机数的值。
5. SV的这套随机稳定性机制，尽管通常不需要我们去做什么，但是要知道：在我们要复现一个执行失败的测试用例的时候，不要改动之前布下的种子，也不要改变程序中线程和对象创建的顺序，避免更改了分层随机种子的顺序

#### 3. 随机稳定性分析
1. 程序随机稳定性
   - $random()如果不带参数设置种子，那么程序每次运行都会获得相同的随机序列，与线程、其他随机无关，每一次得到的随机序列只跟调用的顺序相关，
   - 所以，最好不要用$random()来获取随机数，非要用的话每次都指定不同的随机种子$random(seed)。
2. 线程随机稳定性
   - 同一个线程的随机只受本线程的种子设置、调用顺序影响，不受其他线程的随机影响，即线程间的随机是互不干扰的，**对象的随机也不会干扰线程的随机**；
   - 但是在同一个线程中，$urandom()、$urandom_range()、$std::randomize()、randcase、randsequence、shuffle()是使用同一个随机数发生器的，所以同一线程中它们的调用会相互影响。
3. 对象的随机稳定性
   - 对象的randomize()的随机结果只和对象的种子相关，而和线程的随机、其他对象的随机无关。
     
#### 3. 影响随机结果的因素
1. 种子变化
   - 运行时种子变化（运行时指定或者手动指定种子）后，会影响线程改变点后的所有相关随机。
2. 同一个线程中调用顺序变化
  - 在同一个线程的某个随机调用前增加或者减少urandom()、$urandom_range()、$std::randomize()、randcase、randsequence、shuffle()这些使用同一个随机数发生器的随机，将会使得随机结果发生改变
  - 在一个测试程序中如果希望尽可能的减少对程序的改变，增加的随机最好加在线程的后面。
3. 其他
  - 改变了std::randomize()，randsequence、randcase、$urandom_range()的条目数量、权重、范围都会改变随机结果。

#### 4. 专用名词解释
- 随机数发生器（RNG）

#### 5. eg
~~~

class ictalking;

    rand int rand_var;
    int norand_var;

    function new();
        norand_var = 2021;
    endfunction //new()


    function void test();
            
        logic [7:0] func_local_var;
            
        // 调用 类的内建随机函数 对 类成员变量 进行随机化
        void'(randomize(norand_var));
        $display("The rand_var after randomize() is: %0d", norand_var);
        
        // 调用 类的内建随机函数 对 函数局部变量 进行随机化，但这是非法的，所以注释掉了
        // 如果打开注释会导致编译错误: Argument for randmize() function must be a field of 'this'
        // void'(randomize(func_local_var));
        // $display("The func_local_var after randomize() is %0d", func_local_var);
            
        // 调用 标准随机函数 对 类成员变量 进行随机化
        void'(std::randomize(norand_var));
        $display("The norand_var after std::randomize() is: %0d", norand_var);
            
        // 调用 标准随机函数 对 函数局部变量 进行随机化
        void'(std::randomize(func_local_var));
        $display("The func_local_var after std::randomize() is: %0d", func_local_var);
        
    endfunction // test()


    function int unsigned get_local_randstate();

        process local_process_handler;

        local_process_handler = process::self();
        $display(">> The local process randstate is: %s", local_process_handler.get_randstate());

    endfunction

endclass //ictalking


module random_test;

// Part 1: Probabilistic Distribution System Functions ------------------------------------------
initial begin

    int temp1, temp2;
    int seed;
    
    seed = 20210228;
    for (int i = 0; i < 5; i++) begin
        temp1 = $random; // 可以不带任何参数，使用默认的种子
        temp2 = $dist_uniform(seed, 100, 300); // 可以使用参数来指定种子，还可以指定取值范围
        $display("INDEX: %0d --- RANDOM VALUE: %0d", i, temp1); 
        $display("INDEX: %0d --- DIST_NORMAL VALUE: %0d", i, temp2);
    end
    // 概率分布随机函数支持哪些参数可以具体参考LRM中Probabilistic distribution functions小节
end

// Part 2: Object and Scope Randomization Method ------------------------------------------------
initial begin

    ictalking ictalking_obj;
    #1; // 这一行是为了跟Part1例程的输出区分开

    ictalking_obj = new();
    $display("rand_var   of ictalking_obj after new() is: %0d", ictalking_obj.rand_var);
    $display("norand_var of ictalking_obj after new() is: %0d", ictalking_obj.norand_var);
    
    // 栗子说明：object.randomize只能随机化rand成员变量，不能随机化norand_var
    void'(ictalking_obj.randomize());
    $display("rand_var   of ictalking_obj after object.randomize() is: %0d", ictalking_obj.rand_var);
    $display("norand_var of ictalking_obj after object.randomize() is: %0d", ictalking_obj.norand_var);
    
    // 栗子说明：std::randomize的适用范围更广，类的内建函数randomize()不能随机化局部变量
    ictalking_obj.test();

end

// Part 3: Random Number System Function --------------------------------------------------------
initial begin
    logic [63:0] temp3;
    logic [31:0] temp4;
    #2;
    temp3 = {$urandom(), $urandom()};
    temp4 = $urandom_range(2021, 10086);
    $display("The var temp3 after $urandom is: %0d", temp3);
    $display("The var temp4 after $urandom_range is %0d", temp4);
end

// Part 4: Random Stability ----------------------------------------------------------------------
initial begin
    
    ictalking ictalking_obj2;
    process super_process;
    process child1_process;
    process child2_process;
    process obj2_process;
    string  child1_randstate;
    #3;

    // 通过这个栗子可以看到：各Process的随机状态是独立的，同时种子是分层传递的！
    // 这个栗子中super_process的随机状态跟ictalking_obj2中的局部随机状态一样,这正是因为种子分层传递的原因:
    // 1. 在ictalking_obj2实例化的时候，父线程将当前RNG的随机状态A作为种子传给了ictalking_obj2
    // 2. 然后父线程自己RNG的随机状态自然跳到了下一个随机状态B
    // 3. 当ictalking_obj2调用get_local_randstate函数的时候，它将自己的下一个随机状态B传给了该函数
    // 4. 所以当我们打印出来当前父线程RNG的随机状态和ictalking_obj2的局部随机状态时，会发现时一样的
    ictalking_obj2 = new();
    super_process  = process::self();
    $display(">> The super process randstate is: %s", super_process.get_randstate());
    $display(">> The obj2 process randstate is: %s", ictalking_obj2.get_randstate());
    void'(ictalking_obj2.get_local_randstate());   
    fork
        begin
            child1_process = process::self();
            $display(">> The child1 process randstate is: %s", child1_process.get_randstate());
        end
        begin
            child2_process = process::self();
            $display(">> The child2 process randstate is: %s", child2_process.get_randstate());
        end
    join
    
    // 另外可以通过srandom方法来指定随机种子，比如：
    super_process.srandom(2021);
    ictalking_obj2.srandom(6666);
    
    // 相对应的，有get_randstate就有set_randstate()，通过设置随机状态可以是两个Process产生的随机序列是一样的
    fork
        begin
            child1_process   = process::self();
            child1_randstate = child1_process.get_randstate();
            repeat (5) begin
                $display("Random sequence of child 1 is : %0d", $urandom_range(10, 20));
            end
        end
        begin
            #1;
            child2_process = process::self();
            child2_process.set_randstate(child1_randstate);
            repeat (5) begin
                $display("Random sequence of child 2 is : %0d", $urandom_range(10, 20));
            end
        end
    join

end

endmodule
~~~
结果:
![image](https://github.com/bulaqi/IC-DV.github.io/assets/55919713/01c72e6f-195c-4c67-a6d8-68dd00cf264e)


### 2. 传送门
1. [SystemVerilog | 暗藏玄机的随机化方法](https://zhuanlan.zhihu.com/p/404704966)
2. [SV的随机稳定性](https://zhuanlan.zhihu.com/p/256217124)

